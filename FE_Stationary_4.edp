// Finite Element Method: Time independent Schrödinger equation in 2D
// Infinite Square Well with Conical Perturbation 

real L = 1.0;        // length of the box
real hbar = 1.0545718e-34; // (J·s)
real m    = 9.10938356e-31; // (kg)
real a1=0.5;
real b1=0.3;


int npoints = 40;    // número de puntos de la malla por unidad de longitud
int N = 12;          // number of eigenvalues to calculate
real[int] Evalues(N); // vector to store the eigenvalues
///// BOUNDARY DEFINITIONS /////
border lower(t=0,L) { x = t; y = 0; label=1; };
border right(t=0,L) { x = L; y = t; label=1; };
border upper(t=L,0) { x = t; y = L; label=1; };
border left(t=L,0) { x = 0; y = t; label=1; };

///// CREATE MESH /////
mesh Th = buildmesh(lower(npoints*L*pi) + right(npoints*pi) + upper(npoints*L*pi) + left(npoints*pi));

///// VISUALISE THE MESH /////
plot(Th, wait=1);

///// DEFINE POTENTIAL /////
func real Vtilda(real x, real y) {
    real r = sqrt( ((x - L/2.0)/a1)^2 + ((y - L/2.0)/b1)^2 );
    return 1000*r;
}

///// DECLARE FEM SPACE AND VARIABLES /////
fespace Vh(Th,P2); // espacio de elementos finitos P2
Vh u,v;

///// EVALUATE THE POTENTIAL ON THE FEM SPACE /////
Vh Vmesh;
Vmesh = Vtilda(x,y);

///// PLOT THE POTENTIAL OVER THE MESH /////
plot(Vmesh, value=true, fill=true, wait=1, cmm="Potencial gaussiano");

///// DEFINE THE VARF FOR THE SCHRÖDINGER PROBLEM /////
varf a(u,v) = int2d(Th)(0.5*(dx(u)*dx(v) + dy(u)*dy(v)) + Vtilda(x,y)*u*v)
              + on(1,u=0); // condiciones de Dirichlet

varf b(u,v) = int2d(Th)(u*v);

///// CREATE MATRICES /////
matrix A = a(Vh,Vh,solver=sparsesolver, tgv=-1);
matrix B = b(Vh,Vh, tgv=-1);

///// DECLARE ARRAY TO HOLD EIGENFUNCTIONS /////
Vh[int] Efunctions(N);

///// SOLVE THE EIGENVALUE PROBLEM /////
int k = EigenValue(A, B, sym=true, sigma=0, value=Evalues, vector=Efunctions);

///// PRINT THE EIGENVALUES /////
cout << "Eigenvalues:" << endl;
cout << Evalues << endl;

///// CALCULATE PROBABILITY DENSITIES |ψ|² /////
Vh[int] Eprobability(N);
for (int i=0; i<k; i++) {
    Eprobability[i] = Efunctions[i]^2; // |ψ|²
}

///// PLOT THE FIRST 10 PROBABILITY DENSITIES /////
for (int i=0; i<10 && i<k; i++) {
    plot(Eprobability[i], value=true, fill=true, wait=1, cmm="Densidad de probabilidad #" + i);
}

